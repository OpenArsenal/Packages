# Maintainer: Mohamed Amine Zghal (medaminezghal) <medaminezghal at outlook dot com>

_name=mcp
pkgname=python-${_name}
pkgver=1.26.0
pkgrel=1
pkgdesc='Model Context Protocol SDK (Python library)'
arch=('any')
url='https://github.com/modelcontextprotocol/python-sdk'
license=('MIT')

depends=(
  'python'
  'python-anyio'
  'python-httpx'
  'python-httpx-sse'
  'python-pydantic'
  'python-starlette'
  'python-python-multipart'
  'python-sse-starlette'
  'python-pydantic-settings'
  'uvicorn'
  'python-jsonschema'
  'python-pyjwt'
  'python-cryptography'
  'python-typing_extensions'
  'python-typing-inspection'
)

# Optional: point users to the separate CLI package
optdepends=('mcp: install the MCP CLI tools')

makedepends=(
  'python-hatchling'
  'python-uv-dynamic-versioning'
  'python-build'
  'python-installer'
  'python-wheel'
  'git'
)

checkdepends=(
  'python-pytest'
  'python-trio'
  'python-pytest-xdist'
  'python-pytest-examples'
  'python-inline-snapshot'
  'python-dirty-equals'
  'python-rich'
  'python-typer'
  'python-dotenv'
  'python-websockets'
  'python-requests'
  'ruff'
)

source=(
  "${_name}::git+${url}.git#tag=v${pkgver}"
  "https://github.com/modelcontextprotocol/python-sdk/pull/1834.patch"
)
sha256sums=('eb63712c1da8d41ec45b3290a9ca488e78d99ef913f735514aa058c5b973c634'
            '61524580a527d9c4405d713edcd04d7e263fcc091ebfd4c4a438f75abb749f2d')

prepare() {
  cd "${srcdir}/${_name}"
  git clean -fdx

  # Fix compatibility with Python 3.14
  patch -Np1 -i "${srcdir}/1834.patch"

  # Increase time limit
  sed -i 's/timeout=20/timeout=60/' tests/client/test_config.py
}

build() {
  cd "${srcdir}/${_name}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${srcdir}/${_name}"

  local -a pytest_options=(
    -vv
    --disable-warnings
    -p 'no:benchmark'
  )

  # System deps only (Arch packages), no uv.
  python -m venv --system-site-packages test-env
  ln -sf /usr/bin/ruff test-env/bin/ruff

  test-env/bin/python -P -m installer dist/*.whl
  test-env/bin/python -P -m pytest "${pytest_options[@]}" tests
}

package() {
  cd "${srcdir}/${_name}"

  python -m installer --destdir="${pkgdir}" dist/*.whl

  # IMPORTANT: do NOT ship the CLI script here â€” the `mcp` package will own it.
  rm -f "${pkgdir}/usr/bin/mcp"

  install -Dm644 "${srcdir}/${_name}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
