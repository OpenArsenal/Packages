# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>

pkgname=espeak-ng
pkgver=1.52.0
pkgrel=2
pkgdesc='Multi-lingual software speech synthesizer'
url=https://github.com/espeak-ng/espeak-ng
arch=(x86_64)
license=(GPL-3.0-or-later)
depends=(
  pcaudiolib
  libsonic
)
checkdepends=(python)
makedepends=(
  cmake
  git
  ruby-ronn-ng
)
replaces=(espeak-ng-espeak)
provides=(espeak-ng-espeak)
conflicts=(
  espeak-ng-espeak
  espeak
)
source=(
  "git+$url.git#tag=$pkgver"
  "piper-terminator.patch::https://github.com/espeak-ng/espeak-ng/commit/2108b1e8ae02f49cc909894a1024efdfde6682fd.patch"
)
b2sums=('f934eb948035222de2c6b5c7d8c9e104e0326ceb1052859598ed6f0010fd975bc3e1ef26ac60f77c3b6d1a4aa567b4b7d3c75639dca89eef2508be2a680f7c2b'
        '4d4235bfc48af03d2503f475e126998be79e81a30ee02096cdb1cd542df95fab2421098367ae59e3e253a9dd85382df6571c143f8512e3a60021d784e394c197')

prepare() {
  cd "${srcdir}/${pkgname%-git}"
  patch -Np1 -i "${srcdir}/piper-terminator.patch"
}

build() {
  cmake -B build -S "${pkgname%-git}" -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DFETCHCONTENT_FULLY_DISCONNECTED=ON \
        -DBUILD_SHARED_LIBS=ON \
        -DESPEAK_COMPAT=ON \
        -DESPEAK_BUILD_MANPAGES=ON \
        -Wno-dev
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm -rv "$pkgdir"/usr/include/espeak
}

# vim:set ts=2 sw=2 et:
