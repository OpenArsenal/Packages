# Maintainer: Pedro dev <pedrolucasinvestidor.github@gmail.com>
# Old Maintainer: Radiolin <anton.osi2011@gmail.com>

pkgname=alpaca
_pkgname=Alpaca
pkgver=8.5.1
pkgrel=1
pkgdesc="An Ollama client. Chat with local AI models or connect to third party AI providers like ChatGPT, Gemini and more"
arch=('any')
url="https://github.com/Jeffser/${_pkgname}"
license=('GPL-3.0-or-later')

provides=("alpaca=${pkgver}")
conflicts=('alpaca' 'alpaca-git' 'alpaca-ai')

source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/Jeffser/${_pkgname}/archive/refs/tags/${pkgver}.tar.gz"
)

b2sums=('395ccbaa7a07994180f530cb7e6f9a4bb1142d64311fbff496702ab2f73300083df2f04cc1623ea0abd8c542ccf55a3a8abf39b956e92308762d83ccb808c565')

depends=(
  dconf
  gdk-pixbuf2
  glib2
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libportal
  libspelling
  pango
  vte4
  webkitgtk-6.0

  # spellchecking backends (libspelling uses these)
  libvoikko
  nuspell
  hspell

  # core runtime
  python
  python-gobject
  python-cairo

  # attachments + tools/features (some are repo, some are AUR; PKGBUILD should declare either way)
  python-html2text
  python-matplotlib
  python-odfpy
  python-openai
  python-pillow
  python-pydantic
  python-pydbus
  python-pyicu
  python-pypdf
  python-requests
  python-pytube
  python-youtube-transcript-api
  python-opencv
  python-duckduckgo-search

  # markitdown + converters
  python-markitdown
  python-markdownify
  python-mammoth
  python-pptx
  python-pathvalidate
  python-cobble
  python-magika

  # audio/speech
  python-pydub
  python-pyaudio
  python-speechrecognition
  python-openai-whisper
  gst-plugin-pipewire

  # local backend
  ollama
)

makedepends=(
  appstream
  blueprint-compiler
  desktop-file-utils
  gettext
  gobject-introspection
  meson
)

build() {
  cd "${_pkgname}-${pkgver}"
  arch-meson . build
  meson compile -C build
}

package() {
  cd "${_pkgname}-${pkgver}"
  meson install -C build --destdir "${pkgdir}"
}
