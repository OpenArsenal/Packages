# Maintainer: Okiki <okiki@thunderstrike.co>

pkgname=plezy-bin
_pkgname=plezy
pkgver='1.17.0'
pkgrel=1
pkgdesc="Modern Plex client built with Flutter (prebuilt binary)"
arch=('x86_64' 'aarch64')
url="https://github.com/edde746/plezy"
license=('GPL-3.0-or-later')

depends=('libkeybinder3' 'mpv')

provides=("$_pkgname")
conflicts=("$_pkgname" "${_pkgname}-git")
replaces=("$_pkgname")

# Prebuilt binaries are often already stripped and may contain weird sections.
options=(!strip)

source=(
  "${pkgname%-bin}.desktop"
)
source_x86_64=(
  "$pkgname-$pkgver-x86_64.tar.gz::https://github.com/edde746/plezy/releases/download/${pkgver}/plezy-linux-x64.tar.gz"
)
source_aarch64=(
  "$pkgname-$pkgver-aarch64.tar.gz::https://github.com/edde746/plezy/releases/download/${pkgver}/plezy-linux-arm64.tar.gz"
)

sha256sums=('b8e991c52d0c3b4a703d9232985c6ffd8bddee2120cba9743f4484a82008f050')
sha256sums_x86_64=('e27be48e690681193f4fe6d5cc8ffcccf96d746c567ce121af5e71ad8aff5c67')
sha256sums_aarch64=('9191fd0ec63d75cfb9cc3258b11bae2bb7ceeea619181ae467864c5acab5187a')

package() {
    cd "${srcdir}"

    # Install main application
    install -dm755 "${pkgdir}/opt/${pkgname%-bin}"
    cp -r data lib plezy "${pkgdir}/opt/${pkgname%-bin}/"

    # Make executable
    chmod +x "${pkgdir}/opt/${pkgname%-bin}/plezy"

    # Create symlink for command-line access
    install -dm755 "${pkgdir}/usr/bin"
    ln -s "/opt/${pkgname%-bin}/plezy" "${pkgdir}/usr/bin/${pkgname%-bin}"

    # Install icons in multiple sizes
    for size in 1024x1024 512x512 256x256; do
        install -Dm644 "data/flutter_assets/assets/plezy.png" \
            "${pkgdir}/usr/share/icons/hicolor/${size}/apps/${pkgname%-bin}.png"
    done

    # Install desktop entry
    install -Dm644 "${srcdir}/${pkgname%-bin}.desktop" \
        "${pkgdir}/usr/share/applications/com.edde746.plezy.desktop"
}
