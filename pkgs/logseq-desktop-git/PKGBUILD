# Maintainer: Pi-Yueh Chuang <pychuang@pm.me>
# Contributor: Bader <Bader@unsigned.sh>
pkgname=logseq-desktop-git
pkgver=0.10.12.r9906.e6b5b1e372
pkgrel=1
pkgdesc="A privacy-first, open-source platform for knowledge sharing and management."
arch=("x86_64")
url="https://logseq.com"
repo="https://github.com/logseq/logseq.git"
branch=master # feat/db
dev=n # y
license=("AGPL3")
depends=()
makedepends=("git" "yarn" "npm" "nvm" "clojure" "nodejs>=22")
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}" "${pkgname%-git}-bin")
replaces=("${pkgname%-git}")
source=(
    "${pkgname}::git+${repo}#branch=${branch}"
    "${pkgname%-git}.desktop"
)
md5sums=('SKIP'
         '53b345faf8839be86f1e3a5f9f0db8f1')

pkgver() {
    cd "${srcdir}/${pkgname}"
    printf "%s" "$(git describe --tags --match "[0-9]*.[0-9]*.[0-9]*" --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

_nvm_env() {
  # avoid cluttering user home, while allowing data to be cached
  export HOME="$SRCDEST/node-home"
  export XDG_CACHE_HOME="$HOME/.cache"
  export XDG_CONFIG_HOME="$HOME/.config"
  export XDG_DATA_HOME="$HOME/.local/share"

  export NVM_DIR="$SRCDEST/node-nvm"
  export ELECTRON_SKIP_BINARY_DOWNLOAD=1

  # set up nvm
  source /usr/share/nvm/init-nvm.sh || [[ $? != 1 ]]
  nvm install "${_nodeversion:-lts/*}"
  nvm use "${_nodeversion:-lts/*}"
}

prepare() {
    _nvm_env
    
    cd "${srcdir}/${pkgname}"

    # download required js modules
    yarn install

    # create and sync files to folder `static`
    yarn gulp:build

    # go to folder `static` and download required js modules in static
    cd "${srcdir}/${pkgname}/static"
    yarn install

    # go back to the top-level folder and download clojure dependencies
    cd "${srcdir}/${pkgname}"
    clojure -P -M:cljs
}

build() {
    _nvm_env
    
    cd "${srcdir}/${pkgname}"

    # build
    if [ $dev = "y" ]; then
        yarn cljs:dev-release-electron
    else
        yarn cljs:release-electron
    fi

    # packaging javescript files to an executable
    cd "${srcdir}/${pkgname}/static"
    yarn electron-forge package
}

package() {

    # change the folder permision
    chmod 755 "${srcdir}/${pkgname}/static/out/Logseq-linux-x64"

    # important files are under static/out/Logseq-linux-x64
    cd "${srcdir}/${pkgname}/static/out/Logseq-linux-x64"

    # create destination folder and copy files
    mkdir -p "${pkgdir}/opt/${pkgname}"
    chmod 755 "${pkgdir}/opt/${pkgname}"
    cp -a -r -u --verbose ./ "${pkgdir}/opt/${pkgname}"

    # create a soft link to the executable
    mkdir -p "${pkgdir}/usr/bin"
    ln -s "/opt/${pkgname}/Logseq" "${pkgdir}/usr/bin/logseq"

    # create license folder and make soft links to actual license
    mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -s "/opt/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -s "/opt/${pkgname}/LICENSES.chromium.html" "${pkgdir}/usr/share/licenses/${pkgname}"

    # install readme and additional license file (the top-level AGPL3)
    cd "${srcdir}/${pkgname}"
    install -Dm644 "README.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
    install -Dm644 "LICENSE.md" -t "${pkgdir}/usr/share/licenses/${pkgname}"

    # copy xdg desktop icon
    install -Dm644 "resources/icons/logseq.png" -t "${pkgdir}/usr/share/icons"

    # copy xdg desktop files
    cd "${srcdir}"
    mkdir -p "${pkgdir}/usr/share/applications"
    install -Dm644 "${pkgname%-git}.desktop" -t "${pkgdir}/usr/share/applications"
}
