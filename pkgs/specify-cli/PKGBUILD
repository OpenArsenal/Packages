# Maintainer: Okiki <okiki@thunderstrike.co>
pkgname=specify-cli
pkgver='0.0.95'
pkgrel=1
pkgdesc="Spec Kit Specify CLI (spec-driven development workflow)"
arch=('any')
url='https://github.com/github/spec-kit'
license=('MIT')

depends=(
  'python>=3.12'
  'python-typer'
  'python-rich'
  'python-httpx'
  'python-platformdirs'
  'python-readchar'
  'python-truststore'
  'python-socksio'
)

makedepends=(
  'python-build'
  'python-installer'
  'python-wheel'
  'python-hatchling'
)

source=("$pkgname-$pkgver.tar.gz::https://github.com/github/spec-kit/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('ff0366679e0ffe776fdfc845f30fc919269d70b3aab80457618bcda9e1e03851')

build() {
  cd "spec-kit-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "spec-kit-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # license file location is conventional for Arch packages
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
