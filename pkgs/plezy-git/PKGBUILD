# Maintainer: Okiki <okiki@thunderstrike.co>

pkgname=plezy-git
_pkgname=plezy
pkgver=1.16.1.r1.g358824f
pkgrel=1
pkgdesc="Modern Plex client built with Flutter (git)"
arch=('x86_64')
url="https://github.com/edde746/plezy"
license=('GPL-3.0-or-later')

depends=(
  'gtk3'
  'glib2'
  'libepoxy'
  'libkeybinder3'
  'alsa-lib'
  'mpv'
)

makedepends=(
  'git'
  'clang'
  'cmake'
  'ninja'
  'pkgconf'
  'curl'
  'unzip'
  'flutter'
)

provides=("$_pkgname")
conflicts=("$_pkgname")
replaces=("${pkgname%-git}")

_flutter_ver=3.38.6

source=(
  "$_pkgname::git+$url.git"
  "flutter_linux_${_flutter_ver}-stable.tar.xz::https://storage.googleapis.com/flutter_infra_release/releases/stable/linux/flutter_linux_${_flutter_ver}-stable.tar.xz"
)

sha256sums=(
  'SKIP'
  'SKIP'
)

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long --tags 2>/dev/null | sed 's/^v//;s/-/.r/;s/-/./' \
    || printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  export PATH="$srcdir/flutter/bin:$PATH"
  export PUB_CACHE="$srcdir/.pub-cache"

  flutter config --no-analytics
  flutter precache --linux

  cd "$srcdir/$_pkgname"
  flutter pub get

  # If plugins fail on strict warnings, keep this workaround.
  find . -name 'CMakeLists.txt' -exec sed -i 's/-Werror//g' {} +

  flutter build linux --release
}

package() {
  cd "$srcdir/$_pkgname"

  install -dm755 "$pkgdir/opt/$_pkgname"
  cp -a build/linux/x64/release/bundle/* "$pkgdir/opt/$_pkgname/"

  install -Dm755 /dev/stdin "$pkgdir/usr/bin/$_pkgname" <<EOF
#!/usr/bin/env bash
cd /opt/$_pkgname
exec ./$_pkgname "\$@"
EOF

  install -Dm644 linux/packaging/com.edde746.plezy.desktop \
    "$pkgdir/usr/share/applications/com.edde746.plezy.desktop"

  install -Dm644 assets/plezy.png \
    "$pkgdir/usr/share/icons/hicolor/512x512/apps/$_pkgname.png"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
