# Maintainer: Okiki Ojo <okiki@thunderstrike.co>

pkgname=calibre
pkgver='9.0.0'
pkgrel=1
pkgdesc='Ebook management application'
arch=(x86_64 aarch64)
url=https://calibre-ebook.com
license=(GPL-3.0-only)

provides=(calibre)
conflicts=(calibre-common calibre-python3)

_pydeps=(apsw
         beautifulsoup4
         css-parser
         cssselect
         dateutil
         dnspython
         faust-cchardet
         feedparser
         html2text
         html5-parser
         jeepney
         lxml
         lxml-html-clean
         markdown
         mechanize
         msgpack
         netifaces
         pdftotext
         pillow
         psutil
         py7zr
         pychm
         pycryptodome
         pygments
         pykakasi
         pyqt6
         pyqt6-webengine
         regex
         unrardll
         xxhash
         zeroconf
         zstandard)
depends=(hunspell
         hyphen
         icu
         jxrlib
         libmtp
         libstemmer
         libusb
         libwmf
#        mathjax
         mtdev
         optipng
         podofo
         "${_pydeps[@]/#/python-}"
         qt6-imageformats
         qt6-multimedia
         qt6-speech
         qt6-svg
         qt6-webengine
         ttf-liberation
         uchardet
         udisks2
         zstd
         
         # Piper TTS build needs ONNX Runtime headers + lib + pkg-config.
         # Use the virtual so users can satisfy with cpu/cuda/rocm variants.
         onnxruntime
         ffmpeg)
makedepends=(cmake
             pyqt-builder
             rapydscript-ng
             sip
             xdg-utils

             # Needed at build-time too (headers + libonnxruntime.pc)
             onnxruntime
             ffmpeg)
checkdepends=(poppler
              python-fonttools
              python-pyzstd
              speech-dispatcher
              tk)
optdepends=('poppler: required for converting pdf to html'
            'python-fonttools: required for font subset feature in epub editor'
            'speech-dispatcher: TTS support in the viewer')
conflicts=(calibre-common
           calibre-python3)
replaces=("${conflicts[@]}")
options=('!strip')	

# GitHub tag archives usually unpack as "calibre-<tag>" (tag is "v9.0.0")				
_archive="$pkgname-$pkgver"

# Your sources (mixing patch + binaries - WRONG)
source=(
  "calibre-${pkgver}.tar.gz::https://github.com/kovidgoyal/calibre/archive/refs/tags/v${pkgver}.tar.gz"
  qt-6.10.patch
)
sha256sums=('5bc802e246c63d72b6b9a4499e86c48c56eaa5e1cfbfdeef3e89a315c23c2c7d'
            '7b878663100c38ac849570afc8ee41cfcd55609367a5cb38305ef73bb3094f4e')

prepare() {
  cd "${srcdir}/${_archive}"

	# Desktop integration (e.g. enforce arch defaults)
	# Use uppercase naming scheme, don't delete config files under fakeroot.
	sed -e "/import config_dir/,/os.rmdir(config_dir)/d" \
		  -e "s/'ctc-posml'/'text' not in mt and 'pdf' not in mt and 'xhtml'/" \
		  -e "s/^Name=calibre/Name=Calibre/g" \
		  -i  src/calibre/linux.py

	# Remove unneeded files
	rm -f resources/$pkgname-portable.*

  patch -Np1 -i "${srcdir}/qt-6.10.patch"
}

build() {
  cd "${srcdir}/${_archive}"
	export LANG='en_US.UTF-8'

  # If you're using ffmpeg4.4 compat, its .pc files are under /usr/lib/ffmpeg4.4/pkgconfig.
  # if [[ -d /usr/lib/ffmpeg4.4/pkgconfig ]]; then
  #   export PKG_CONFIG_PATH="/usr/lib/ffmpeg4.4/pkgconfig:/usr/lib/pkgconfig"
  # fi

	python setup.py build
	python setup.py iso639
	python setup.py iso3166

	# the liberation fonts directory is deleted later, but this step is necessary to prevent the setup from downloading them
	python setup.py liberation_fonts --system-liberation_fonts \
				 --path-to-liberation_fonts /usr/share/fonts/liberation

	#	MathJax 4 not supported
	#	python setup.py mathjax --system-mathjax --path-to-mathjax /usr/share/mathjax
	python setup.py mathjax
	python setup.py gui
}

check() {
  cd "${srcdir}/${_archive}"
	export LANG='en_US.UTF-8'

	# Skip piper test since we disabled it
	python setup.py test --under-sanitize --exclude-test-name test_piper
	python setup.py test_rs
}

package() {
  cd "${srcdir}/${_archive}"
	export LANG='en_US.UTF-8'

	# If this directory doesn't exist, zsh completion won't install.
	install -d "$pkgdir/usr/share/zsh/site-functions"

  # staging root should be $pkgdir; prefix decides /usr inside it
	python setup.py install \
		    --staging-root="$pkgdir/usr" \
		    --prefix=/usr \
		    --system-plugins-location=/usr/share/calibre/system-plugins

  install -d "${pkgdir}/usr/share/man"
	cp -a man-pages/. "$pkgdir/usr/share/man"
	
	# Remove build artifacts
	rm -r "$pkgdir/usr/share/calibre/rapydscript/"
	rm -r "$pkgdir/usr/share/calibre/fonts/liberation/"

	# Compiling bytecode FS#33392
	# This is kind of ugly but removes traces of the build root.
	while read -rd '' _file; do
		_destdir="$(dirname "${_file#${pkgdir}}")"
		python -m compileall -d "${_destdir}" "${_file}"
		python -O -m compileall -d "${_destdir}" "${_file}"
	done < <(find "$pkgdir/usr/lib/" -name '*.py' -print0)
}
